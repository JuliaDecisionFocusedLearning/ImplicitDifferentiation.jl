struct DefaultLinearSolver end

function (::DefaultLinearSolver)(A, b::AbstractVector)
    x, stats = gmres(A, b)
    return x
end

function (::DefaultLinearSolver)(A, B::AbstractMatrix)
    # X, stats = block_gmres(A, B)  # https://github.com/JuliaSmoothOptimizers/Krylov.jl/issues/854
    X = mapreduce(hcat, eachcol(B)) do b
        first(gmres(A, b))
    end
    return X
end

"""
    ImplicitFunction{lazy}

Wrapper for an implicit function defined by a forward mapping `y` and a set of conditions `c`.

An `ImplicitFunction` object behaves like a function, and every call is differentiable with respect to the first argument `x`.
When a derivative is queried, the Jacobian of `y` is computed using the implicit function theorem:

    ∂/∂y c(x, y(x)) * ∂/∂x y(x) = -∂/∂x c(x, y(x))

This requires solving a linear system `A * J = -B` where `A = ∂c/∂y`, `B = ∂c/∂x` and `J = ∂y/∂x`.

# Fields

- `forward`: a callable computing `y(x)`, does not need to be compatible with automatic differentiation
- `conditions`: a callable computing `c(x, y)`, must be compatible with automatic differentiation
- `linear_solver`: a callable to solve the linear system `A * J = -B`
- `conditions_x_backend`: defines how the conditions will be differentiated with respect to the first argument `x` 
- `conditions_y_backend`: defines how the conditions will be differentiated with respect to the second argument `y` 

# Type parameters

- `lazy`: whether to use a `LinearOperator` from [LinearOperators.jl](https://github.com/JuliaSmoothOptimizers/LinearOperators.jl) (`lazy = true`) or a dense Jacobian matrix (`lazy = false`) for `A`

# Constructors

    ImplicitFunction{lazy}(
        forward, conditions;
        linear_solver, conditions_x_backend, conditions_x_backend
    )

    ImplicitFunction(
        forward, conditions;
        linear_solver, conditions_x_backend, conditions_x_backend
    )

Default values:

- `lazy = true`
- `linear_solver`: the direct solver `\` for dense Jacobians, or an iterative solver from [Krylov.jl](https://github.com/JuliaSmoothOptimizers/Krylov.jl) for lazy operators
- `conditions_x_backend = nothing`
- `conditions_y_backend = nothing`

# Function signatures

There are two possible signatures for `forward` and `conditions`, which must be consistent with one another:
    
| standard | byproduct |
|:---|:---|
| `forward(x, args...; kwargs...) = y`      | `conditions(x, y, args...; kwargs...) = c` |
| `forward(x, args...; kwargs...) = (y, z)` | `conditions(x, y, z, args...; kwargs...) = c` |
    
In both cases, `x`, `y` and `c` must be `AbstractVector`s, with `length(y) = length(c)`.
In the second case, the byproduct `z` can be an arbitrary object generated by `forward`.
The positional arguments `args...` and keyword arguments `kwargs...` must be the same for both `forward` and `conditions`.

The byproduct `z` and the other positional arguments `args...` beyond `x` are considered constant for differentiation purposes.

# Linear solver

The provided `linear_solver` objects needs to be callable, with two methods:
- `(A, b::AbstractVector) -> s::AbstractVector` such that `A * s = b`
- `(A, B::AbstractVector) -> S::AbstractMatrix` such that `A * S = B` 

# Condition backends

The provided `conditions_x_backend` and `conditions_y_backend` can be either:
- `nothing`, in which case the outer backend (the one differentiating through the `ImplicitFunction`) is used
- an object subtyping `AbstractADType` from [ADTypes.jl](https://github.com/SciML/ADTypes.jl).
"""
struct ImplicitFunction{
    lazy,F,C,L,B1<:Union{Nothing,AbstractADType},B2<:Union{Nothing,AbstractADType}
}
    forward::F
    conditions::C
    linear_solver::L
    conditions_x_backend::B1
    conditions_y_backend::B2
end

function ImplicitFunction{lazy}(
    forward::F,
    conditions::C;
    linear_solver::L=lazy ? DefaultLinearSolver() : \,
    conditions_x_backend::B1=nothing,
    conditions_y_backend::B2=nothing,
) where {lazy,F,C,L,B1,B2}
    return ImplicitFunction{lazy,F,C,L,B1,B2}(
        forward, conditions, linear_solver, conditions_x_backend, conditions_y_backend
    )
end

function ImplicitFunction(forward, conditions; kwargs...)
    return ImplicitFunction{true}(forward, conditions; kwargs...)
end

function Base.show(io::IO, implicit::ImplicitFunction{lazy}) where {lazy}
    (; forward, conditions, linear_solver, conditions_x_backend, conditions_y_backend) =
        implicit
    return print(
        io,
        "ImplicitFunction{$(lazy ? "lazy" : "dense")}($forward, $conditions, $linear_solver, $conditions_x_backend, $conditions_y_backend)",
    )
end

"""
    (implicit::ImplicitFunction)(x::AbstractArray, args...; kwargs...)

Return `implicit.forward(x, args...; kwargs...)`, which can be either an `AbstractVector` `y` or a tuple `(y, z)`.

This call makes `y` differentiable with respect to `x`.
"""
function (implicit::ImplicitFunction)(x::AbstractVector, args...; kwargs...)
    y_or_yz = implicit.forward(x, args...; kwargs...)
    return y_or_yz
end

output(y::AbstractVector) = y
byproduct(::AbstractVector) = error("No byproduct")

output(yz::Tuple{<:Any,<:Any}) = yz[1]
byproduct(yz::Tuple{<:Any,<:Any}) = yz[2]

output((y, z)) = y
byproduct((y, z)) = z
