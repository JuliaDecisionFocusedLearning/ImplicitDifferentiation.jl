struct DefaultLinearSolver end

function (::DefaultLinearSolver)(A, b::AbstractVector)
    x, stats = gmres(A, b)
    return x
end

function (::DefaultLinearSolver)(A, B::AbstractMatrix)
    # X, stats = block_gmres(A, B)  # https://github.com/JuliaSmoothOptimizers/Krylov.jl/issues/854
    X = mapreduce(hcat, eachcol(B)) do b
        first(gmres(A, b))
    end
    return X
end

"""
    ImplicitFunction

Wrapper for an implicit function defined by a forward mapping `y` and a set of conditions `c`.

An `ImplicitFunction` object behaves like a function, and every call is differentiable with respect to the first argument `x`.
When a derivative is queried, the Jacobian of `y` is computed using the implicit function theorem:

    ∂/∂y c(x, y(x)) * ∂/∂x y(x) = -∂/∂x c(x, y(x))

This requires solving a linear system `A * J = -B` where `A = ∂c/∂y`, `B = ∂c/∂x` and `J = ∂y/∂x`.

# Fields

- `forward`: a callable, does not need to be compatible with automatic differentiation
- `conditions`: a callable, must be compatible with automatic differentiation
- `linear_solver`: a callable with two methods:
    - `(A, b::AbstractVector) -> s::AbstractVector` such that `A * s = b`
    - `(A, B::AbstractVector) -> S::AbstractMatrix` such that `A * S = B` 
- `conditions_x_backend`: either `nothing` or a subtype of `ADTypes.AbstractADType`, defines how the conditions will be differentiated with respect to the first argument `x` 
- `conditions_y_backend`: same for the second argument `y`

There are two possible signatures for `forward` and `conditions`, which must be consistent with one another:
    
1. Standard: `forward(x, args...; kwargs...) = y` and `conditions(x, y, args...; kwargs...) = c`
2. Byproduct: `forward(x, args...; kwargs...) = (y, z)` and `conditions(x, y, z, args...; kwargs...) = c`.
    
In both cases, `x`, `y` and `c` must be vectors, with `length(y) = length(c)`.
In the second case, the byproduct `z` can be an arbitrary object generated by `forward`.
The positional arguments `args...` and keyword arguments `kwargs...` must be the same for both `forward` and `conditions`.

!!! warning "Warning"
    The byproduct `z` and the other positional arguments `args...` beyond `x` are considered constant for differentiation purposes.
"""
@kwdef struct ImplicitFunction{
    F,C,L,B1<:Union{Nothing,AbstractADType},B2<:Union{Nothing,AbstractADType}
}
    forward::F
    conditions::C
    linear_solver::L = DefaultLinearSolver()
    conditions_x_backend::B1 = nothing
    conditions_y_backend::B2 = nothing
end

function ImplicitFunction(forward, conditions; kwargs...)
    return ImplicitFunction(; forward, conditions, kwargs...)
end

function Base.show(io::IO, implicit::ImplicitFunction)
    (; forward, conditions, linear_solver, conditions_x_backend, conditions_y_backend) =
        implicit
    return print(
        io,
        "ImplicitFunction($forward, $conditions, $linear_solver, $conditions_x_backend, $conditions_y_backend)",
    )
end

"""
    (implicit::ImplicitFunction)(x::AbstractArray, args...; kwargs...)

Return `implicit.forward(x, args...; kwargs...)`, which can be either a vector `y` or a tuple `(y, z)`.

This call is differentiable (except for `z`).
"""
function (implicit::ImplicitFunction)(x::AbstractVector, args...; kwargs...)
    y_or_yz = implicit.forward(x, args...; kwargs...)
    return y_or_yz
end

output(y::AbstractVector) = y
byproduct(::AbstractVector) = error("No byproduct")

output((y, z)) = y
byproduct((y, z)) = z
